##
# federc/main.fd
#-------------------------------------------------------------------------------
# Copyright (c) 2018 Fionn Langhans
#
# This software is provided 'as-is', without any express or implied warranty. In
# no event will the authors be held liable for any damages arising from the use
# of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it freely,
# subject to the following restrictions:
# 
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software in a
#    product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
#
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 
# 3. This notice may not be removed or altered from any source distribution.
#-------------------------------------------------------------------------------
#
# This file is part of the Feder compiler. It is the main
# file of the compiler and should be compiled as the main
# file.
##

include "stdio.fd"
include "federc/compile.fd"
include "federc/help.fd"
include "federc/license.fd"
include "federc/pretty_printer.fd"

if args.length() == 1
    compiler.help.err()
    return false
;

global FEDERC_VERSION = "1.0"

cargs = String[0]
# Ignore the file argument
for i = 1, i < args.length(), i++
    append(cargs, String from args.at(i))
;

if isEqual(String from args.at(1), "help")
    # Help was requested
    if args.length() == 2
        compiler.help.general()
        return true
    ;

    if args.length() > 3
        # Invalid usage: Should only be 3 arguments
        io.err.println("Usage:")
        io.err.println("    federc help [operation]")
        return false
    ;

    if isEqual(String from args.at(2), "compile")
        # Operation: compile
        compiler.help.compile()
        return true
    else if isEqual(String from args.at(2), "style")
        # Operation: style
        compiler.help.style()
        return true
    else if isEqual(String from args.at(2), "license")
        # Operation: license
        compiler.help.license()
        return true
    ;

    io.err.println("Help:")
    io.err.println("    federc help")
    return false
else if isEqual(String from args.at(1), "compile")
    if !compiler.compile.command(cargs)
        return false
    ;

    return true
else if isEqual(String from args.at(1), "style")
    # Give 'cargs' to the command interpreter of
    # the pretty printer
    if !compiler.pretty.command(cargs)
        return false
    ;

    return true
else if isEqual(String from args.at(1), "license")
    # Print the license of this program
    compiler.federc.license()
    return true
else if isEqual(String from args.at(1), "version")
    # Print version of this program
    io.print("federc version ")
    io.println(FEDERC_VERSION)
    return true
;

compiler.help.err()
return false
